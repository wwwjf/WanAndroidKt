plugins {
    id 'kotlin-android'
    id 'kotlin-kapt'
}
if (isLibrary.toBoolean()){
    apply plugin: 'com.android.library'
} else {
    apply plugin:  'com.android.application'
}

android {
    compileSdkVersion Integer.valueOf(compile_sdk_version)
    buildToolsVersion "$build_tools_version"


    defaultConfig {
        if (!isLibrary.toBoolean()){
            applicationId "com.wwwjf.jetpackdemo"
        }

        minSdkVersion Integer.valueOf(min_sdk_version)
        targetSdkVersion Integer.valueOf(target_sdk_version)
        versionCode Integer.valueOf(version_code)
        versionName "$version_name"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding = true
        viewBinding true
    }


    sourceSets{
        main{
            if (isLibrary.toBoolean()){
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            }
        }
    }

    signingConfigs {
//        签名文件信息
        config {
            keyAlias 'epay'
            keyPassword 'EpayApp13579'
            storeFile file('epay.jks')
            storePassword 'EpayApp13579'
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            //发布地址 http://devgitlab.epay.br.com/plus/epay_app_release.git
            def fileName
            if (variant.buildType.name == "debug") {
                fileName = "./jetpack_debug_v${defaultConfig.versionName}.apk"

            } else {
                fileName = "./jetpack_test_v${defaultConfig.versionName}_${getGitVersion()}.apk"

            }
            outputFileName = fileName

        }
    }

}


def getGitVersion(){
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.legacy:legacy-support-v4:$legacy_support_version"

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-common:$room_version"

    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$navigation_version"
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$ext_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core_version"

}